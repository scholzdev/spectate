plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
    id "xyz.jpenilla.run-paper" version "2.3.1"
}

group = 'dev.florianscholz'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { name = "papermc-repo"; url = "https://repo.papermc.io/repository/maven-public/" }
    maven { name = "jitpack"; url = 'https://jitpack.io' }
}
dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.3-R0.1-SNAPSHOT")
    compileOnly("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")
    implementation group: 'com.github.twitch4j', name: 'twitch4j', version: '1.24.0'
}

java {
    sourceCompatibility = JavaVersion.toVersion(21)
    targetCompatibility = JavaVersion.toVersion(21)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(21)
}

// ----------------------------------------------------
// ShadowJar (Dependencies relocaten & zusammenpacken)
// ----------------------------------------------------
shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.runtimeClasspath]
    mergeServiceFiles()

    relocate 'com.github.twitch4j', 'shaded.com.github.twitch4j'
    relocate 'com.fasterxml.jackson', 'shaded.com.fasterxml.jackson'
    relocate 'com.github.philippheuer', 'shaded.com.github.philippheuer'
    relocate 'io.github.xanthic.cache', 'shaded.io.github.xanthic.cache'
    relocate 'com.bucket4j', 'shaded.com.bucket4j'
    relocate 'org.slf4j', 'shaded.org.slf4j'
    relocate 'org.apache.commons', 'shaded.org.apache.commons'
    relocate 'com.neovisionaries', 'shaded.com.neovisionaries'
    relocate 'io.github.resilience4j', 'shaded.io.github.resilience4j'
}

tasks.named('build') {
    dependsOn 'shadowJar'
}
